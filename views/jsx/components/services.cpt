ServicesList = React.createClass

  getInitialState: ->
    state =
      keyword: null
      type: null
      services: null
    return state

  fetchServices: ->
    params =
      keyword: @state.keyword
      type: @state.type
    Teresa.postJSON '/service/fetch', params, (response) =>
      if response.status is 'OK'
        @setState
          services: response.services
      return
    return

  updateKeyword: ->
    keyword = $(@refs.keyword.getDOMNode()).val()
    @setState
      keyword: keyword
    , =>
      @fetchServices()
      return
    return

  clearKeyword: ->
    @setState
      keyword: null
    , =>
      @fetchServices()
      return
    return

  componentDidMount: ->
    @fetchServices()
    return

  render: ->
    <div className="card">
      <div className="card-header">
        <div className="row">
          <div className="col-sm-8">
            <h5 className="card-title card-title--with-input">
              Available Services
              {
                if @state.keyword?
                  " - \'#{@state.keyword}\' "
              }
              {
                if @state.keyword?
                  <a href="javascript:;" onClick={@clearKeyword}>reset search</a>
              }
            </h5>
          </div>
          <div className="col-sm-4">
            <div className="input-group">
              <input type="text" className="form-control" placeholder="Search for..." ref="keyword" name="keyword" />
              <span className="input-group-btn">
                <button className="btn btn-secondary" type="button" onClick={@updateKeyword}>Go!</button>
              </span>
            </div>
          </div>
        </div>
      </div>
      <div className="card-block">
        {
          if @state.services? and @state.services.length > 0
            <div className="row">
            {
              @state.services.map (service) =>
                <ServicesListItem
                  service={service}
                  handleChooseService={@props.handleChooseService}
                />
            }
            </div>
        }
      </div>
    </div>

ServicesListItem = React.createClass

  getInitialState: ->
    state = {}
    return state

  render: ->
    <div className="col-md-6">
      <div className="card">
        <div className="card-block">
          <h3 className="card-title">{@props.service.name}</h3>
          <p className="text-muted">{@props.service.description}</p>
          <p className="tag tag-default">{@props.service.type.charAt(0).toUpperCase() + @props.service.type.slice(1)}</p>
          <p><a className="btn btn-primary" href="javascript:;" onClick={@props.handleChooseService.bind @, @props.service}>Select Service</a></p>
        </div>
      </div>
    </div>